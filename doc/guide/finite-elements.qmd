---
engine: julia
---

# Finite elements

```{julia}
#| include: false
include("setup.jl")

using Latexify
using Symbolics
using LaTeXStrings
using CairoMakie: Axis, Figure
```

Variables and helper function

```{julia}
@variables a b x y l EA EI ν q

function prettyprint(name, factor, value)
    s1 = name.s[2:end-1]
    s2 = latexify(simplify(factor), env=:raw)
    s3 = latexify(simplify.(1 / factor * value, expand=true), env=:raw)
    return L"%$s1 = %$s2 %$s3"
end;
```

## 1D basis functions

### Piecewise linear hat functions

```{julia}
φs = hatfunctions([0, 0.25, 0.75, 1.5, 2.0])

fig = Figure()
fplot!(Axis(fig[1, 1], title="Functions"), φs...)
fplot!(Axis(fig[2, 1], title="Derivatives"), derivative.(φs)...)
fig
```

Linear combination

```{julia}
uhat = [0.0, 0.25, 0.125, 0.865, 0.75]
uh = uhat ⋅ φs

fplot(uh, uh', zero(uh))
```

### Piecewise functions with continuous derivatives

#### Standard Hermite polynomials

The common approach (see e.g. Fish and Belytschko: A first course in finite elements) is to use piecewise cubic shape functions 

```{julia}
Φ = makeelement(:hermite, 0 .. 6) |> nodalbasis |> components
fig = Figure()
fplot!(Axis(fig[1, 1], title="Functions"), Φ...)
fplot!(Axis(fig[2, 1], title="Derivatives"), derivative.(Φ)...)
fig
```

and to combine them into the basis functions

```{julia}
xs = [0, 1, 4, 5]
φs = hermitefunctions(xs)

fig = Figure()
fplot!(Axis(fig[1, 1], title="Functions"), φs...)
fplot!(Axis(fig[2, 1], title="Derivatives"), derivative.(φs)...)
fig
```

Any linear combination is then a $C^1$ function.

```{julia}
uhat = [1, 1, 0, -0.25, 0, 0.25, 1, -1]
uh = uhat ⋅ φs

xd = []
yd = []
for x = xs
    push!(xd, x, x + 1, x + 1, x, NaN)
    push!(yd, uh(x), uh(x) + uh'(x), uh(x), uh(x), NaN)
end

p = fplot(uh, uh', zero(uh))
scatter!(xs, uhat[1:2:end], color=:tomato)
scatter!(xs, uhat[2:2:end], color=:blue)
lines!(xd, yd)
p
```

Note that the coefficient vector $\hat{\mathbf{u}}$ contains function values and derivatives for each node.

#### Extended Hermite polynomials

The exact solution for a beam with constant line load is a polynomial of degree four. In order to be able to compute exact solutions in this case, an additional internal node can be introduced.

```{julia}
xs = [0, 5, 10]
φs = hermitefunctions(xs, midnode=true)
fplot(φs)
```

## Elements

```{julia}
fig = Figure(size=(700, 700))
Axis(fig)
feplot(fig[1, 1], makeelement(:lagrange, IHat, k=1)) |> feconf()
feplot(fig[1, 2], makeelement(:lagrange, IHat, k=3)) |> feconf()
feplot(fig[2, 1], makeelement(:hermite, IHat)) |> feconf()
feplot(fig[2, 2], makeelement(:lagrange, QHat, k=1)) |> feconf()
feplot(fig[3, 1], makeelement(:lagrange, QHat, k=2)) |> feconf()
feplot(fig[3, 2], makeelement(:serendipity, QHat, k=2)) |> feconf()
feplot(fig[4, 1], makeelement(:hermite, QHat, conforming=true)) |> feconf()
feplot(fig[4, 2], makeelement(:hermite, QHat, conforming=false)) |> feconf()
fig
```

## Shape functions

Linear shape functions

```{julia}
makeelement(:lagrange, IHat, k=1) |> nodalbasis |> components |> fplot
```

Hermite polynomials

```{julia}
Ns = nodalbasis(makeelement(:hermite, 1 .. 4)) |> components
fig = Figure()
fplot!(Axis(fig[1, 1], title="Functions"), Ns...)
fplot!(Axis(fig[2, 1], title="Derivatives"), derivative.(Ns)...)
fig
```

Bilinear shape functions

```{julia}
#| include: false
GLMakie.activate!()
```

```{julia}
makeelement(:lagrange, QHat, k=1) |> nodalbasis |> components |> fplot3d
```

Serendipity shape functions

```{julia}
makeelement(:serendipity, (0 .. 2) × (0 .. 1), k=2) |> nodalbasis |> components |> fplot3d
```

Conforming rectangular plate element shape functions

```{julia}
makeelement(:hermite, QHat) |> nodalbasis |> components |> fplot3d
```

Quadratic serendipity shape functions on the domain $[0, a] \times [0, b]$

```{julia}
K = (0 .. a) × (0 .. b)
Ns = nodalbasis(makeelement(:serendipity, K, k=2))

prettyprint(L"\mathbf{N}(x, y)", 1 / (a^2 * b^2), [Nᵢ(x, y) for Nᵢ ∈ Ns])
```


## Finite element stiffness matrices

### Truss

```{julia}
Ns = nodalbasis(makeelement(:lagrange, IHat, k=1))
D(u) = 2 / l * u'
aᵉ(u, δu) = EA * l / 2 * integrate(D(u) * D(δu), IHat)
Kᵉ = [aᵉ(Nᵢ, Nⱼ) for Nᵢ ∈ Ns, Nⱼ ∈ Ns]

prettyprint(L"\mathbf{K}^e", EA / l, Kᵉ)
```

### Beam

```{julia}
Ns = [1, l / 2, 1, l / 2] .* nodalbasis(makeelement(:hermite, IHat))
D(w) = 4 / l^2 * w''
aᵉ(w, δw) = EI * l / 2 * integrate(D(w) * D(δw), IHat)
Kᵉ = [aᵉ(Nᵢ, Nⱼ) for Nᵢ ∈ Ns, Nⱼ ∈ Ns]

prettyprint(L"\mathbf{K}^e", EI / l^3, Kᵉ)
```

```{julia}
bᵉ(δw) = q * l / 2 * integrate(δw, IHat)
rᵉ = [bᵉ(Nᵢ) for Nᵢ ∈ Ns]

prettyprint(L"\mathbf{r}^e", q / 12, rᵉ)
```

### 2D Poisson equation

Integration on the reference quadrilateral

```{julia}
Ns = nodalbasis(makeelement(:lagrange, QHat, k=1))
D(u) = [2 / a * ∂x(u), 2 / b * ∂y(u)]
aᵉ(u, δu) = a * b / 4 * integrate(D(u) ⋅ D(δu), QHat)
Kᵉ = [aᵉ(Nᵢ, Nⱼ) for Nᵢ ∈ Ns, Nⱼ ∈ Ns]

prettyprint(L"\mathbf{K}^e", 1 / (6 * a * b), Kᵉ)
```

Alternative derivation in physical coordinates

```{julia}
K = (0 .. a) × (0 .. b)
Ns = nodalbasis(makeelement(:lagrange, K, k=1))
aᵉ(u, δu) = integrate(∇(u) ⋅ ∇(δu), K)
Kᵉ = [aᵉ(Nᵢ, Nⱼ) for Nᵢ ∈ Ns, Nⱼ ∈ Ns]

prettyprint(L"\mathbf{K}^e", 1 / (6 * a * b), Kᵉ)
```

```{julia}
bᵉ(δu) = integrate(δu, K)
rᵉ = [bᵉ(Nᵢ) for Nᵢ ∈ Ns]

prettyprint(L"\mathbf{r}^e", a * b / 4, rᵉ)
```


## Finite element solutions

```{julia}
function plotbeamsol(EI, wh)
    fig = Figure()
    fplot!(Axis(fig[1, 1], title=L"Displacement $w(x)$"), wh, zero(wh), npoints=100)
    fplot!(Axis(fig[2, 1], title=L"Bending moment $M(x)$"), -EI * wh'', zero(wh))
    fplot!(Axis(fig[3, 1], title=L"Shear force $V(x)$"), -EI * wh''', zero(wh))
    return fig
end
```

### Beam on two supports with tip force

```{julia}
l = 2.5
EI = 10
F = 1.0
xa = 3 * l / 4
xs = [0, xa, l]
φs = hermitefunctions(xs)

aa(w, δw) = EI * ∫(w'' * δw'') + 1e6 * (w(0) * δw(0) + w'(0) * δw'(0) + w(xa) * δw(xa))
bb(δw) = F * δw(l)

K = [aa(φi, φj) for φi = φs, φj = φs]
r = [bb(φi) for φi = φs]

what = K \ r
wh = what ⋅ φs

plotbeamsol(EI, wh)
```

### Cantilever beam with line load

```{julia}
l = 2.5
EI = 10
q = 1.0
xs = [0, l]
φs = hermitefunctions(xs, midnode=true)

aa(w, δw) = EI * ∫(w'' * δw'') + 1e6 * (w(0) * δw(0) + w'(0) * δw'(0))
bb(δw) = q * ∫(δw)

K = [aa(φi, φj) for φi = φs, φj = φs]
r = [bb(φi) for φi = φs]

what = K \ r
wh = what ⋅ φs

plotbeamsol(EI, wh)
```
